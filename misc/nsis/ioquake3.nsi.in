; NSIS description file for Spearmint installer
;
; you have to copy SDL.dll and OpenAL32.dll here manually

!define VERSION "XXXVERSIONXXX"
!define RELEASE "XXXRELEASEXXX"

!define MODDIR "baseq3"
!define MODDIR2 "missionpack"
!define PUBLISHER "The Spearmint Team"
!define URL "http://ioq3ztm.googlecode.com/"

!define GAMENAME "Spearmint"
!define CLIENT "spearmint.x86.exe"
!define SERVER "spearmint-server.x86.exe"
!define GAMEDIR "Spearmint"
!define GAMEKEY "spearmint"

!define MULTIUSER_MUI
!define MULTIUSER_EXECUTIONLEVEL Highest
!define MULTIUSER_INSTALLMODE_COMMANDLINE
!define MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_KEY "Software\${GAMEKEY}"
!define MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_VALUENAME "Install_Mode"
!define MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_KEY "Software\${GAMEKEY}"
!define MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_VALUENAME "Install_Dir"
!define MULTIUSER_INSTALLMODE_INSTDIR "${GAMEDIR}"
!include MultiUser.nsh

!include "MUI2.nsh"
!define MUI_ICON "../quake3.ico"

; The name of the installer
Name "${GAMENAME} ${VERSION}"

; The file to write
OutFile "XXXFILENAMEXXX"

; The default installation directory
; set by Multiuser.nsh
;InstallDir "$PROGRAMFILES\${GAMEDIR}"

; Registry key to check for directory (so if you install again, it will
; overwrite the old one automatically)
; handled by Multiuser.nsh
;InstallDirRegKey HKLM "Software\${GAMEKEY}" "Install_Dir"

;--------------------------------
;Interface Settings

!define MUI_ABORTWARNING

;--------------------------------
;Pages

!insertmacro MULTIUSER_PAGE_INSTALLMODE
;!insertmacro MUI_PAGE_LICENSE "../../COPYING.txt"
!define MUI_COMPONENTSPAGE_NODESC
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

;--------------------------------
;Languages

!insertmacro MUI_LANGUAGE "English"

;--------------------------------

;--------------------------------
;Multiuser stuff
Function .onInit
  !insertmacro MULTIUSER_INIT
FunctionEnd

Function un.onInit
  !insertmacro MULTIUSER_UNINIT
FunctionEnd

; The stuff to install
Section "${GAMENAME} ${VERSION}-${RELEASE} (required)"

  SectionIn RO

  ; Set output path to the installation directory.
  SetOutPath $INSTDIR

  File "SDL.dll"
!ifndef USE_INTERNAL_SPEEX
  File "libspeex.dll"
!endif
!ifndef USE_INTERNAL_ZLIB
  File "zlib1.dll"
!endif
!ifndef USE_INTERNAL_JPEG
  File "jpeg8c.dll"
!endif

  ; Put file there
  File "../../build/release-mingw32-x86/${SERVER}"
  File "../../build/release-mingw32-x86/${CLIENT}"
!ifdef USE_RENDERER_DLOPEN
  File "../../build/release-mingw32-x86/renderer_opengl1_x86.dll"
  File "../../build/release-mingw32-x86/renderer_rend2_x86.dll"
!endif

  File "../../COPYING.txt"
  File "/oname=README.txt" "../../README"
  File "../../id-readme.txt"
  File "../../voip-readme.txt"

  SetOutPath "$INSTDIR\${MODDIR}"
  File "../../build/release-mingw32-x86/${MODDIR}/cgamex86.dll"
  File "../../build/release-mingw32-x86/${MODDIR}/gamex86.dll"
  File "../../build/release-mingw32-x86/${MODDIR}/uix86.dll"
!ifdef MODDIR2
  SetOutPath "$INSTDIR\${MODDIR2}"
  File "../../build/release-mingw32-x86/${MODDIR2}/cgamex86.dll"
  File "../../build/release-mingw32-x86/${MODDIR2}/gamex86.dll"
  File "../../build/release-mingw32-x86/${MODDIR2}/uix86.dll"
!endif

  ; Write the installation path into the registry
  WriteRegStr SHCTX "Software\${GAMEKEY}" ${MULTIUSER_INSTALLMODE_INSTDIR_REGISTRY_VALUENAME} "$INSTDIR"
  WriteRegStr SHCTX "Software\${GAMEKEY}" ${MULTIUSER_INSTALLMODE_DEFAULT_REGISTRY_VALUENAME} "$MultiUser.InstallMode"

  ; Write the uninstall keys for Windows
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "DisplayName" "${GAMENAME}"
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "DisplayVersion" "${VERSION}-${RELEASE}"
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "Publisher" "${PUBLISHER}"
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "URLInfoAbout" "${URL}"
  WriteRegStr SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegDWORD SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "NoModify" 1
  WriteRegDWORD SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}" "NoRepair" 1
  WriteUninstaller "uninstall.exe"

SectionEnd

; Optional section (can be disabled by the user)
Section "Start Menu Shortcuts"

  CreateDirectory "$SMPROGRAMS\${GAMENAME}"
  CreateShortCut "$SMPROGRAMS\${GAMENAME}\Uninstall.lnk" "$INSTDIR\uninstall.exe" "" "$INSTDIR\uninstall.exe" 0
  CreateShortCut "$SMPROGRAMS\${GAMENAME}\${GAMENAME}.lnk" "$INSTDIR\${CLIENT}" "" "$INSTDIR\${CLIENT}" 0

SectionEnd

!ifdef USE_OPENAL_DLOPEN
Section "OpenAL-Soft library"

  SetOutPath $INSTDIR

  File "OpenAL32.dll"

SectionEnd
!endif

!ifdef USE_CURL_DLOPEN
Section "libcurl"

  SetOutPath $INSTDIR

  File "libcurl-4.dll"

SectionEnd
!endif

;--------------------------------

; Uninstaller

Section "Uninstall"

  ; Remove registry keys
  DeleteRegKey SHCTX "Software\Microsoft\Windows\CurrentVersion\Uninstall\${GAMEKEY}"
  DeleteRegKey SHCTX "Software\${GAMEKEY}"

  ; Remove files and uninstaller
  Delete $INSTDIR\${MODDIR}\cgamex86.dll
  Delete $INSTDIR\${MODDIR}\gamex86.dll
  Delete $INSTDIR\${MODDIR}\uix86.dll
!ifdef MODDIR2
  Delete $INSTDIR\${MODDIR2}\cgamex86.dll
  Delete $INSTDIR\${MODDIR2}\gamex86.dll
  Delete $INSTDIR\${MODDIR2}\uix86.dll
!endif
  Delete $INSTDIR\${CLIENT}
  Delete $INSTDIR\${SERVER}
!ifdef USE_RENDERER_DLOPEN
  Delete $INSTDIR\renderer_opengl1_x86.dll
  Delete $INSTDIR\renderer_rend2_x86.dll
!endif

  Delete $INSTDIR\COPYING.txt
  Delete $INSTDIR\README.txt
  Delete $INSTDIR\id-readme.txt
  Delete $INSTDIR\voip-readme.txt

  Delete $INSTDIR\SDL.dll
!ifndef USE_INTERNAL_SPEEX
  Delete $INSTDIR\libspeex.dll
!endif
!ifndef USE_INTERNAL_ZLIB
  Delete $INSTDIR\zlib1.dll
!endif
!ifndef USE_INTERNAL_JPEG
  Delete $INSTDIR\jpeg8c.dll
!endif

!ifdef USE_OPENAL_DLOPEN
  Delete $INSTDIR\OpenAL32.dll
!endif
!ifdef USE_CURL_DLOPEN
  Delete $INSTDIR\libcurl-4.dll
!endif

  Delete $INSTDIR\uninstall.exe

  ; Remove shortcuts, if any
  Delete "$SMPROGRAMS\${GAMENAME}\Uninstall.lnk"
  Delete "$SMPROGRAMS\${GAMENAME}\${GAMENAME}.lnk"

  ; Remove directories used
  RMDir "$SMPROGRAMS\${GAMENAME}"
  RMDir "$INSTDIR\${MODDIR}"
!ifdef MODDIR2
  RMDir "$INSTDIR\${MODDIR2}"
!endif
  RMDir "$INSTDIR"

SectionEnd
